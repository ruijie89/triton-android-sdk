apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def releaseGroupId = 'sg.com.sph.android'
def releaseArtifactId = "${rootProject.name}"
def releaseRepoKey = 'libs-release-local'
def snapshotRepoKey = 'libs-snapshot-local'

task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId releaseGroupId
            version = rootProject.ext.tritonSDKVersion
            artifactId releaseArtifactId

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact sourceJar

            artifact("$buildDir/outputs/aar/${archivesBaseName}-${tritonSDKVersion}.aar")

            pom.withXml {

                asNode().appendNode("description", description)

                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

artifactory {
    contextUrl = gcp_artifactory_url
    publish {
        repository {
            // The Artifactory repository key to publish to
            version = rootProject.ext.tritonSDKVersion
            repoKey = version.toString().endsWith('SNAPSHOT') ? snapshotRepoKey : releaseRepoKey

            username = gcp_artifactory_user
            password = gcp_artifactory_pass
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'q.os': 'android']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}